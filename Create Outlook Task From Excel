Option Explicit

Sub createTask()

    Dim emailApplication As Object
    Dim taskItem As Object
    Dim lastRow As Long, rowNumber As Long
    
    Set emailApplication = CreateObject("Outlook.Application")

    On Error GoTo errHandler
    
    lastRow = Cells(Rows.Count, "A").End(xlUp).Row
    
    If 2 > lastRow Then Exit Sub
        
    For rowNumber = 2 To lastRow
        
        If Len(Cells(rowNumber, "A").Value) > 0 Then
            
            Set taskItem = emailApplication.CreateItem(3) 'https://docs.microsoft.com/en-us/office/vba/api/Outlook.OlItemType
            
            With taskItem
                .Subject = Cells(rowNumber, "A").Value
                .DueDate = Cells(rowNumber, "B").Value
                .body = Cells(rowNumber, "C").Value
                .Save 'very important to save task
            End With
            
            Set taskItem = Nothing
            
        End If
        
    Next rowNumber
    
    Set emailApplication = Nothing
    Set taskItem = Nothing
    VBA.MsgBox "created task successfully.", vbInformation
        
    Exit Sub
    
errHandler:
    Set emailApplication = Nothing
    Set taskItem = Nothing
    VBA.MsgBox "ERROR", vbCritical
End Sub


Excel sheet i used to create Task
         A       B                C
1    Steps	Start Date	Body
2    Step 1	12-01-2022	some note of step 1
3    Step 2	13-01-2022	some note of step 2
4    Step 3	14-01-2022	some note of step 3
5    Step 4	15-01-2022	some note of step 4
6    Step 5	16-01-2022	some note of step 5

